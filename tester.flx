// Temporarily, test script in top level of repo

// To print the time in generated files
// Include subroutines
include "./flx/lib/winterfell_public";
include "./flx/lib/parse_wlog";
include "./flx/lib/cairo_verifier";
include "web/low_res_time";
var tod = #{
  open LowResTime;
  return #time_t.gmtime.asctime.[to -1] + " UTC"; // time of day for stamp
};

// ************************************************************* 
// Filenames
// Aero input and output

fun / (x:string, y:string) => Filename::join (x,y);

var input_path_f = "examples/example";
begin 
  var f = System::argv 1;
  if f == "--help" do
    println$ "Usage: flx tester.flx testfile/sans/extension" ;
    System::exit 0;
  done
  if f != "" perform
    input_path_f = f;
end

var stamp = "// TESTCASE: " + input_path_f + " AT: "  + tod ;

proc stamp_file(f:string) {
  println$ "STAMPING " + f;
  var inp = load f;
  println$ "File length " + inp.len.str;
  save(f,stamp + "\n" + inp);
}


// INPUTS
var input_air_f = input_path_f + ".air";
var winterfell_main_f = input_path_f + "_winterfell_main.rs"; // Hard coded top level Winterfell mainline for this test

// FIRST STAGE OUTPUTS
var generated_cairo_f = input_path_f + ".cairo";
var generated_rust_f = input_path_f + ".rs";

println$ "[tester] processing " + input_air_f + " and " + winterfell_main_f;
println$ "[tester] generating " + generated_cairo_f + " and " + generated_rust_f
;

// FIXED Winterfell inputs
var file_to_copy_winterfell_main_to_f = "examples/tests/winterfell/src/main.rs"; 
var file_to_copy_generated_rust_to_f = "examples/tests/cairo/example.rs"; 

// FIXED Winterfell outputs
var public_names_f = "example.public";
var log_filename_f = "example.wlog";

// FIXED Protostar inputs
var protostar_test_filename_f = "examples/tests/cairo/test_air.cairo"; // generated by this Felix script
var file_to_copy_generated_cairo_to_f = "examples/tests/cairo/example.cairo"; // generated by Aero

 
// ************************************************************* 
// 1, RUN AIRSCRIPT 
begin 
  var result = System::system$ "cargo run --bin airc -- transpile -i " + input_air_f;
  println$ "AIRSCIPT " + input_air_f " + result = " + result.str;
  if result != 0 perform System::exit result;
  stamp_file generated_cairo_f;
  stamp_file generated_rust_f;
end


// ************************************************************* 
// BUILD AND RUN HACKED WINTERFELL FOR THIS AIR
// We do this for two reasons:
//
// 1. To get data out of the prover the verifier needs
// 2. To get values out of the Rust verifier to check our cairo one
//
// The hacked Winterfell contains log outputs which we subsequently
// parse to get the requisite values.


// ************************************************************* 
// 2. COPY WINTERFELL VERIFIER (Rust code)
begin
  var result = System::system$ "cp " + generated_rust_f + " "  +  file_to_copy_generated_rust_to_f;
  println$ "COPIED example.rs" + result.str;
  if result != 0 perform System::exit result;
end

// ************************************************************* 
// 3. COPY WINTERFELL HAND CODED MAIN (Rust code)
begin
  var result = System::system$ "cp " +winterfell_main_f + " "  +  file_to_copy_winterfell_main_to_f;
  println$ "winterfell main " + result.str;
  if result != 0 perform System::exit result;
end

// ************************************************************* 
// 4. RUN WINTERFELL to get required data and results for comparison
begin
  var result = System::system "RUSTFLAGS=-Awarnings cargo run --bin winterfell --release";
  println$ "WINTERFELL result = " + result.str;
  if result != 0 perform System::exit result;
end

// ************************************************************* 
// 5a. PARSE PUBLIC INPUT NAMES FILE
var pub_data = parse_winterfell_public(public_names_f); 

// 5b. PARSE WINTERFELL GENERATED LOG FILE 
var parse_data = parse_wlog(log_filename_f);


// ************************************************************* 
// 6. GENERATE THE CAIRO VERIFIER and SAVE IT
begin
  var output = gen_cairo_verifier parse_data pub_data;
  save(protostar_test_filename_f, output);
end

// ************************************************************* 
// 7. COPY VERIFIER TO WHERE PROTOSTAR CAN FIND IT
begin 
  var result = System::system ("cp " + generated_cairo_f + " " + file_to_copy_generated_cairo_to_f);
  println$ "CP result = " + result.str;
end

// ************************************************************* 
// 8. RUN PROTOSTAR CAIRO VERIFIER TEST
begin
  var result = System::system "protostar --no-color -p unit test";
  println$ "PROTOSTAR result = " + result.str;
end


